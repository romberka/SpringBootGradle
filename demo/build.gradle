buildscript {
    repositories {
        mavenCentral()
    }
    
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE')
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'demo'
    version =  '1.0'
}

sourceSets {
    main {
         java {
          srcDirs += "$buildDir/generated"
         }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
     mavenCentral()
}

ext {
    springBootVersion = '1.5.4.RELEASE'
    cxfVersion = '3.1.12'
    cxfArtifacts = [
        'cxf-rt-frontend-jaxws',
        'cxf-rt-frontend-jaxrs',
        'cxf-rt-transports-http',
        'cxf-rt-rs-client',
        'cxf-rt-rs-service-description'
    ]
    cxfToolArtifacts = [
        'cxf-tools-wsdlto-frontend-jaxws',
        'cxf-tools-wsdlto-databinding-jaxb',
        'cxf-tools-common',
        'cxf-tools-wsdlto-core'
    ]
    wsdlDir = file("src/main/wsdl")
    generatedWsdlDir = file("$buildDir/generated")
	wsdlsToGenerate = [
        ["$wsdlDir/RTC922_FetchBusinessTechnicalMasterData_AccountDetailQuery.wsdl"]
	]
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
//    compile "org.springframework.boot:spring-boot-starter-security:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-activemq:$springBootVersion"
    compile "org.apache.qpid:qpid-jms-client:0.23.0"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    compile "org.springframework.boot:spring-boot-devtools:$springBootVersion"
    compile "org.apache.commons:commons-lang3:3.6"
    compile "com.github.ulisesbocchio:jasypt-spring-boot-starter:1.12"
    compile "org.webjars:bootstrap:3.3.4"
    compile "org.webjars:jquery:2.1.4"

    compile "org.apache.cxf:cxf-spring-boot-starter-jaxws:3.1.12"
    cxfArtifacts.each { artifact ->
        compile "org.apache.cxf:$artifact:$cxfVersion"
    }

    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion") {
		exclude(module: 'commons-logging')
    }
}

configurations { cxfTool }

dependencies {
    cxfToolArtifacts.each { artifact ->
        cxfTool "org.apache.cxf:$artifact:$cxfVersion"
    }
}

task wsdl2java() {
	if (wsdlDir.listFiles()) {
		inputs.files wsdlDir.listFiles()
		outputs.files generatedWsdlDir
	    outputs.upToDateWhen { false }
		
		doLast {
            wsdlsToGenerate.each { argsin ->
                argsin.add(argsin.size - 1, '-d')
                argsin.add(argsin.size - 1, generatedWsdlDir)
                javaexec {
                    classpath configurations.cxfTool
                    main = 'org.apache.cxf.tools.wsdlto.WSDLToJava'
                    args = argsin
                    systemProperties = ['exitOnFinish':'TRUE']
                }
            }
		}
	}
}

compileJava.dependsOn wsdl2java
